%import common.WS
%import common.NEWLINE
%import common.ESCAPED_STRING
%ignore WS

ASTERISK : "*"
EQUAL : "="
OPENSQ : "["
CLOSEDSQ : "]"
CLOSEDCURLY : "}"
COLON : ":"
HASHMARK : "#"
INTEGER : ( "0".."9" )+
OPENCURLY : "{"
SLASH : "/"
QUOTATION_MARK : "\""

// QKEY is a quoted key.
QKEY : (OPENSQ | QUOTATION_MARK ) BKEY (CLOSEDSQ | QUOTATION_MARK)
// BKEY is a bare candidate key.
BKEY : ((("a".."z"|"A".."Z") ("a".."z"|"A".."Z"|"0".."9")* ) | ESCAPED_STRING )
// QVALUE is a quoted value.
// TODO: allow for any characters inside QVALUE
QVALUE : "\"" ("a".."z"|"A".."Z"|" "|"0".."9")+ "\""

?start: abiffile

?abiffile : ( comment | json_line | cand_line | voteline )+

// comment
comment : HASHMARK comment_string NEWLINE
comment_string : /[^\r\n]+/

// json_line - This is a subset of JSON that only allows for key/value pairs. 
json_line : ( OPENCURLY [json_pair] CLOSEDCURLY ) comment?
json_pair   : json_key ":" ( QVALUE | QKEY )
json_key: ( QKEY | BKEY )

// cand_line
cand_pair   : cand_key ":" ( QVALUE | QKEY )
cand_key: ( QKEY | BKEY )
cand_line : ( EQUAL? cand_key cand_id_sep ( cand_square_quoted | cand_key ) ) comment?
cand_square_quoted : OPENSQ /[^\]]+/ CLOSEDSQ
cand_id_sep : COLON

// voteline
voteline : ballot_count count_sep cands_in_voteline comment?
ballot_count : INTEGER
?cands_in_voteline : ( cands_in_voteline cand_sep cand_voter_pref | cand_voter_pref )
cand_voter_pref : ( cand_id | cand_tok_rating )
cand_tok_rating : cand_id SLASH rating
cand_sep : ( eq | gt )
eq : ( "=" | "," )
gt : ">"
rating : ("0".."9")+
count_sep : COLON
cand_id : ( cand_key | cand_square_quoted )

